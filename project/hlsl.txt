// Object3d.hlsli

struct VertexShaderOutput
{
    float4 position : SV_POSITION;
    float2 texcoord : TEXCOORD0;
    float3 normal : NORMAL0;
};



// Object3d.PS.hlsl

#include "object3d.hlsli"

struct Material
{
    float4 color;
    int enableLighting;
    float4x4 uvTransform;
};

struct DirectionalLight
{
    float4 color; // ライトの色
    float3 direction; // ライトの向き
    float intensity; // 輝度
};

ConstantBuffer<Material> gMaterial : register(b0);
Texture2D<float4> gTexture : register(t0);
SamplerState gSampler : register(s0);
ConstantBuffer<DirectionalLight> gDirectionalLight : register(b1);

struct PixelShaderOutput
{
    float4 color : SV_TARGET0;
};

PixelShaderOutput main(VertexShaderOutput input)
{
    PixelShaderOutput output;
    
    float4 transformedUV = mul(float4(input.texcoord, 0.0f, 1.0f), gMaterial.uvTransform);
    float4 textureColor = gTexture.Sample(gSampler, transformedUV.xy);
    
    float3 normal = normalize(input.normal);
    float3 lightDir = normalize(-gDirectionalLight.direction);
    float NdotL = dot(normal, lightDir);
    float lighting = 1.0f;

    if (gMaterial.enableLighting == 1)
    {
        // Lambert
        lighting = saturate(NdotL);
    }
    else if (gMaterial.enableLighting == 2)
    {
        // Half Lambert
        lighting = pow(NdotL * 0.5f + 0.5f, 2.0f);
    }
    else
    {
        // No Lighting
        lighting = 1.0f;
    }

    float3 finalColor = gMaterial.color.rgb * textureColor.rgb * lighting * gDirectionalLight.color.rgb * gDirectionalLight.intensity;
    
    if (gMaterial.enableLighting == 0)
    {
    // 完全にアンリット（照明無視）
        finalColor = gMaterial.color.rgb * textureColor.rgb;
    }
    else
    {
        finalColor.rgb = gMaterial.color.rgb * textureColor.rgb * lighting * gDirectionalLight.color.rgb * gDirectionalLight.intensity;
    }
    
    output.color = float4(finalColor, 1.0f);
    return output;
}



// Object3d.VS.hlsl

#include "object3d.hlsli"

struct TransformationMatrix
{
    float4x4 WVP;
    float4x4 World;
};

ConstantBuffer<TransformationMatrix> gTransformationMatrix : register(b0);

struct VertexShaderInput
{
    float4 position : POSITION0;
    float2 texcoord : TEXCOORD0;
    float3 normal : NORMAL0;
};

VertexShaderOutput main(VertexShaderInput input)
{
    VertexShaderOutput output;
    output.position = mul(input.position, gTransformationMatrix.WVP);
    output.texcoord = input.texcoord;
    output.normal = normalize(mul(input.normal, (float3x3) gTransformationMatrix.World));
    return output;
}



// Sprite.hlsli

struct VSIn
{
    float3 pos : POSITION;
    float2 uv : TEXCOORD0;
};
struct VSOut
{
    float4 svpos : SV_POSITION;
    float2 uv : TEXCOORD0;
};



// Sprite.Ps.hlsl

#include "Sprite.hlsli"

// C++側の並びと完全一致させる（colorの直後に行列）
cbuffer MaterialPS : register(b0)
{
    float4 gColor;
    row_major float4x4 gUV; // 行優先でCPUの行列と一致
}

Texture2D gTex : register(t0);
SamplerState gSamp : register(s0);

float4 main(VSOut pin) : SV_TARGET
{
    float4 uvh = mul(float4(pin.uv, 0, 1), gUV);
    return gTex.Sample(gSamp, uvh.xy) * gColor;
}



// Sprite.VS.hlsl

#include "Sprite.hlsli"

cbuffer TransformVS : register(b0)
{
    row_major float4x4 gWVP;
}

VSOut main(VSIn vin)
{
    VSOut o;
    o.svpos = mul(float4(vin.pos, 1), gWVP);
    o.uv = vin.uv;
    return o;
}



// Particle.hlsli

struct VertexShaderOutput
{
    float4 position : SV_POSITION;
    float2 texcoord : TEXCOORD0;
    float4 color : COLOR0;
};



// Particle.PS.hlsl

#include "Particle.hlsli"

struct Material
{
    float4 color;
    int enableLighting;
    float4x4 uvTransform;
};

ConstantBuffer<Material> gMaterial : register(b0);
Texture2D<float4> gTexture : register(t0);
SamplerState gSampler : register(s0);

struct PixelShaderOutput
{
    float4 color : SV_TARGET0;
};

PixelShaderOutput main(VertexShaderOutput input)
{
    PixelShaderOutput output;
    float4 uv = mul(float4(input.texcoord, 0.0f, 1.0f), gMaterial.uvTransform);
    float4 tex = gTexture.Sample(gSampler, uv.xy);
    float4 col = input.color * gMaterial.color * tex;

    if (col.a <= 0.0f)
        discard;

    output.color = col;
    return output;
}


// Particle.VS.hlsl

#include "Particle.hlsli"

struct ParticleForGPU
{
    float4x4 WVP;
    float4x4 World;
    float4 color;
};

StructuredBuffer<ParticleForGPU> gParticle : register(t1);

struct VertexShaderInput
{
    float3 position : POSITION0;
    float2 texcoord : TEXCOORD0;
    // billboard 板ポリ用。NORMAL/COLOR は使わない
};

VertexShaderOutput main(VertexShaderInput input, uint instanceId : SV_InstanceID)
{
    VertexShaderOutput output;
    float4 pos = float4(input.position, 1.0f);
    output.position = mul(pos, gParticle[instanceId].WVP);
    output.texcoord = input.texcoord;
    output.color = gParticle[instanceId].color;
    return output;
}